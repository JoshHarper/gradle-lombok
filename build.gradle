plugins {
    id 'com.jfrog.bintray' version '1.3.1'
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven'
    id 'jacoco'
}

group 'net.franz-becker'
version currentVersion

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'com.netflix.nebula:nebula-test:3.0.0'
    testCompile 'org.apache.commons:commons-lang3:3.4'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'gradle-lombok-plugin'
        licenses = ['Apache-2.0']
        version {
            name = "${project.version}"
        }
    }
}

pluginBundle {
    website = 'https://github.com/franzbecker/gradle-lombok'
    vcsUrl = 'https://github.com/franzbecker/gradle-lombok.git'
    description = 'A gradle plugin that simplifies the usage of the Lombok annotation processor.'
    tags = ['gradle', 'lombok']

    plugins {
        lombokPlugin {
            id = 'net.franz-becker.gradle-lombok'
            displayName = 'Gradle Lombok plugin'
        }
    }
}

task wrapper(type: Wrapper, description: 'Gradle Wrapper task') {
    gradleVersion = '2.6'
}

task updateReadmeVersion << {
    def versionPattern = /\d+.\d+(.\d+)?/
    def encoding = 'UTF-8'
    File file = new File("README.md")

    // String replacements - Readme isn't long enough to justify advanced code ;)
    String text = file.getText(encoding)
    text = text.replaceAll("gradle-lombok' version '${versionPattern}'", "gradle-lombok' version '${project.version}'")
    text = text.replaceAll("gradle-lombok/${versionPattern}", "gradle-lombok/${project.version}")

    file.setText(text, encoding)
}
