package net.franz_becker.gradle.lombok.task

import net.franz_becker.gradle.lombok.AbstractIntegrationTest

/**
 * Integration tests for {@link DelombokTask}.
 */
class DelombokTaskIntegrationTest extends AbstractIntegrationTest {

    def "Calling delombok with --help throws no exception."() {
        given:
        buildFile << """
            import net.franz_becker.gradle.lombok.task.DelombokTask

            task delombok(type: DelombokTask) {
                args "--help"
            }
        """.stripIndent()

        when:
        runTasksSuccessfully("delombok")

        then:
        noExceptionThrown()
    }

    def "Delombok on directory produced proper output"() {
        given:
        createJavaSource()

        and:
        buildFile << """
            import net.franz_becker.gradle.lombok.task.DelombokTask

            task delombok(type: DelombokTask) {
                args("src/main/java", "-d", "src/gen/java")
            }
        """.stripIndent()

        when:
        runTasksSuccessfully("delombok")

        then:
        File file = new File(projectDir, "src/gen/java/com/example/HelloWorld.java")
        file.exists()
        String contents = file.text
        !contents.contains("import")
        contents.contains("// Generated by delombok")
        contents.contains("public String getId() {")
    }

    private void createJavaSource() {
        def file = createFile("src/main/java/com/example/HelloWorld.java")
        file << """
            package com.example;

            import lombok.Data;

            @Data
            public class HelloWorld {

                private String id;

            }
        """.stripIndent()
    }

}
